" ----------------------------------------------------------------------------

if filereadable(expand("$HOME/.asdf/shims/python3"))
  let g:python3_host_prog="$HOME/.asdf/shims/python3"
endif

if (!empty($TMUX_PANE)) && (!empty(v:servername))
  call system("mkdir -p $HOME/.cache/nvim-sockets")
  call system("ln -fs " . v:servername . " $HOME/.cache/nvim-sockets/tmux-pane-$TMUX_PANE")

  augroup autocom
    autocmd VimLeave * call system("rm $HOME/.cache/nvim-sockets/tmux-pane-$TMUX_PANE")
  augroup END
endif

augroup vimrc
  autocmd!
  autocmd TextYankPost * silent! lua vim.highlight.on_yank()
augroup END

let $VIMHOME=expand("$HOME/.vim")
let $FT_DIR=expand("$VIMHOME/after/ftplugin/")
let $MANPAGER=""

set nocompatible

" Extend runtimepath with plugin directories ---------------------------------

function! ExtendRuntimepath()
  for plugin_path in split(globpath(expand('~/.usr/plugins/vim'), '*'))
    if 0 != isdirectory(plugin_path) && -1 == match(&runtimepath, plugin_path)
      execute 'set runtimepath+=' . plugin_path
      let l:after_path = plugin_path . '/after'
      if isdirectory(l:after_path)
        execute 'set runtimepath+=' . l:after_path
      endif
    endif
  endfor
endfunction

call ExtendRuntimepath()

lua << EOF
  require("CopilotChat").setup {
    model = "claude-opus-4.5"
  }
EOF

" Highlighting ---------------------------------------------------------------

syntax on
if hostname() == 'atom'
  colorscheme mbr-dark
else
  colorscheme mbr-light
endif

""" highlight trailing whitespace
highlight ExtraWhitespace ctermbg=1
autocmd BufWinEnter,InsertEnter,InsertLeave * match ExtraWhitespace / \|\(\s\+$\)/
autocmd BufWinLeave * call clearmatches()

" Behavior -------------------------------------------------------------------

set hidden " restore history of buffer coming from background
set nomodeline
set clipboard=unnamed,unnamedplus
set ttimeoutlen=100  " decrease timeout for faster insert with 'O'
set ttyfast
set lazyredraw
set shell=~/.zsh/run_script_with_zshrc
set nofoldenable
set diffopt+=internal,algorithm:patience
set nobackup
set nowritebackup
set notermguicolors  " TODO: update colorscheme

" Input ----------------------------------------------------------------------

set nrformats-=octal " No octal number handling with C-a and C-x
set cpoptions=B$ " cw-like commands mark end with $ instead of deleting.
set tildeop
set backspace=2
set spelllang=de,en
set nospell
set mouse=n

" UI -------------------------------------------------------------------------

set t_Co=256
set colorcolumn=81
set cursorline
set cursorcolumn
set noerrorbells
let &visualbell = has('mac')
set t_vb=
set scrolloff=3 " scroll three lines before window start / end
set showcmd
set ruler " show cursor position
set hlsearch " highlight search results
set incsearch " highlight search while entering search string
set display=lastline " display parts of the last line
set listchars=tab:→ 
set list " enable listchars

" Format ---------------------------------------------------------------------

set encoding=utf-8
set autoindent
set formatoptions+=j
set formatoptions-=o
set shiftround " When at 3 spaces and I hit >>, go to 4, not 5.
set lisp " '-' is a word character
set expandtab
set shiftwidth=2
set softtabstop=2

" Completion -----------------------------------------------------------------

set wildmenu
set wildignore+=.DS_Store
set wildignore+=*.orig
set wildignore+=*.pyc
set wildignore+=*.o
set wildignore+=*.class
set wildignore+=.hg
set wildignore+=.git
set wildignore+=.svn
set wildignore+=tmp
set wildignore+=.beam

" Completion menu also with omni completion
set completeopt=longest,menuone

if executable('ag')
  " Use ag over grep
  set grepprg=ag\ --nogroup\ --nocolor

  " Use ag in CtrlP for listing files. Lightning fast and respects .gitignore
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'

  " ag is fast enough that CtrlP doesn't need to cache
  let g:ctrlp_use_caching = 0
endif

" Functions ------------------------------------------------------------------

function! RenameFile()
  let old_name = expand('%')
  let new_name = input('New file name: ', old_name)
  if new_name != '' && new_name != old_name
    if system('git ls-files ' . old_name . ' 2> /dev/null') == ""
      execute ':saveas ' . new_name
      execute ':silent !rm ' . old_name
      redraw!
    else
      call system('git mv ' . old_name . ' ' . new_name . ' 2> /dev/null')
      let l:alternative_file = @#
      execute 'edit ' . new_name
      let @# = l:alternative_file
    endif
    execute 'bd ' . old_name
  endif
endfunction

function! SetCHeaderFiletype()
  let file_no_extension=expand("%:p:r")
  if filereadable(file_no_extension . ".cpp")
    set filetype=cpp
  elseif filereadable(file_no_extension . ".c")
    set filetype=c
  elseif filereadable(file_no_extension . ".m")
    set filetype=objc
  endif
endfunction

function! SourceLocalVimrc()
  let local_vimrc = findfile('.vimrc', ';')
  if local_vimrc != "" && expand('~/.vimrc') != fnamemodify(local_vimrc, ':p')
    execute "source " . fnamemodify(local_vimrc, ':p')
  endif
endfunction

function! VisualSelection()
  try
    let a_save = @a
    normal! gv"ay
    return @a
  finally
    let @a = a_save
  endtry
endfunction

function! QuickFixBuffersOfTab()
  return filter(tabpagebuflist(), {i, buf -> getbufvar(buf, '&filetype') == 'qf'})
endfunction

function! HasQuickfixWindow()
  let l:windows = gettabinfo(tabpagenr())[0]["windows"]
  for l:winnr in l:windows
    let l:wininfo = getwininfo(l:winnr)[0]
    if l:wininfo['quickfix'] && !l:wininfo['loclist']
      return 1
    endif
  endfor
  return 0
endfunction

function! ToggleLocations()
  cclose
  if empty(QuickFixBuffersOfTab())
    CocDiagnostics
    " lopen
  else
    lclose
  endif
endfunction

function! ToggleQuickfix()
  lclose
  if empty(QuickFixBuffersOfTab())
    copen
  else
    cclose
  endif
endfunction

lua << EOF
local function close_other_buffers()
  local keep_bufs = {}

  for _, tab in ipairs(vim.api.nvim_list_tabpages()) do
    for _, win in ipairs(vim.api.nvim_tabpage_list_wins(tab)) do
      local buf = vim.api.nvim_win_get_buf(win)
      keep_bufs[buf] = true

      local alt_buf = vim.fn.getwinvar(vim.api.nvim_win_get_number(win), '#')
      if type(alt_buf) == 'number' and alt_buf > 0 then
        keep_bufs[alt_buf] = true
      end
    end
  end

  local global_alt = vim.fn.bufnr('#')
  if global_alt > 0 then
    keep_bufs[global_alt] = true
  end

  for _, buf in ipairs(vim.api.nvim_list_bufs()) do
    if vim.api.nvim_buf_is_loaded(buf) and not keep_bufs[buf] then
      vim.api.nvim_buf_delete(buf, { force = false })
    end
  end
end

vim.api.nvim_create_user_command('CleanBuffers', close_other_buffers, {})
EOF

" Filetype stuff -------------------------------------------------------------

filetype plugin on
filetype plugin indent on

augroup filetypedetect
  " This group is created by Vim, so don't use 'autocmd!'.

  autocmd BufRead,BufNewFile *.m set filetype=objc
  autocmd BufRead,BufNewFile *.h call SetCHeaderFiletype()

  autocmd BufRead,BufNewFile Jenkinsfile set syntax=groovy
augroup END

" Mappings -------------------------------------------------------------------

let mapleader = ","

nnoremap <Leader>n :TagbarClose<CR>:NERDTreeFind<CR>
nnoremap <Leader>N :TagbarClose<CR>:NERDTreeToggle<CR>
nnoremap <Leader>ü :NERDTreeClose<CR>:TagbarToggle<CR>
nnoremap <Leader>b :CtrlPBuffer<CR>
nnoremap <Leader>t :CtrlP<CR>
nnoremap <Leader>T :CtrlPTag<CR>
nnoremap <Leader>p :call ctrlp#paste#start()<CR>
nnoremap <Leader>r :call RenameFile()<CR>
nnoremap <Leader># :call ctrlp#find_alternatives#start(expand("%"))<cr>
nnoremap <Leader>' :rightbelow vsplit #<CR>
nnoremap <Leader>y :let @* = expand("%") . ":" . line(".")<CR>

nnoremap <Leader>cc :call ChangeWord('ToCamelCaseLower')<CR>
nnoremap <Leader>cC :call ChangeWord('ToCamelCaseUpper')<CR>
nnoremap <Leader>cs :call ChangeWord('ToSnakeCaseLower')<CR>
nnoremap <Leader>cS :call ChangeWord('ToSnakeCaseUpper')<CR>
nnoremap <Leader>cm :call ChangeWord('ToMinusCase')<CR>

nnoremap <Leader>f :call system("pbcopy", expand("%"))<CR>
nnoremap <Leader>F :call system("pbcopy", expand("%:p"))<CR>

nnoremap <Leader>l :call ToggleLocations()<cr>
nnoremap <Leader>L :lclose<CR>
nnoremap <Leader>q :call ToggleQuickfix()<cr>
nnoremap <Leader>Q :cclose<CR>
" call CocAction('fillDiagnostics', bufnr('%'))
nnoremap <F6> :if HasQuickfixWindow() \| cprevious \| else \| lprevious \| endif<cr>
nnoremap <F7> :if HasQuickfixWindow() \| cnext \| else \| lnext \| endif<cr>

nnoremap <Leader>mL :call Tmux_run_command("")<CR>
nnoremap <Leader>ms :call Tmux_run_command("git status")<CR>
nnoremap <Leader>mD :call Tmux_run_command("git diff")<CR>
nnoremap <Leader>md :call Tmux_run_command("git diff '" . expand('%') . "'")<CR>
nnoremap <Leader>ml :call Tmux_run_command("tig '" . expand('%') . "'")<CR>
nnoremap <Leader>mb :call Tmux_run_command("tig blame '" . expand('%') . "' +" . line('.'))<CR>

let maplocalleader = "ö"

nnoremap <LocalLeader>; :execute "normal! mqA;\e`q"<cr>
nnoremap <LocalLeader>rr :call Save_buffer_if_modified()<cr>:call Run_file(expand('%'))<cr>
nnoremap <LocalLeader>rn :call Save_buffer_if_modified()<cr>:call Run_file_line(expand('%'), line('.'))<cr>
nnoremap <LocalLeader>ri :call Save_buffer_if_modified()<cr>:call Run_interactive_file(expand('%'))<cr>
nnoremap <LocalLeader>rh :call ctrlp#run#start()<CR>
nnoremap <LocalLeader>rl :call Save_buffer_if_modified()<cr>:call Run_last_command()<cr>
nnoremap  <LocalLeader>f :lua vim.lsp.buf.format()<cr> \| :w<cr>
" nmap <LocalLeader>gD <Plug>(coc-definition)
" nmap <LocalLeader>gT <Plug>(coc-type-definition)
" nmap <LocalLeader>gI <Plug>(coc-implementation)
" nmap <LocalLeader>gR <Plug>(coc-references)
" nnoremap <LocalLeader>ö :call CocActionAsync('doHover')<cr>
" nnoremap <LocalLeader>h :call CocAction('doHover')<cr>
" nnoremap <LocalLeader>a :CocAction codeaction<CR>
" nnoremap <LocalLeader>c :Copilot panel<CR>

function! Snippets_shebang(key)
  let l:values = {
        \  'ruby': '#! /usr/bin/env ruby',
        \  'python': '#! /usr/bin/env python3',
        \  'bash': '#! /usr/bin/env bash',
        \  'sh': '#! /bin/sh',
        \}
  if has_key(l:values, &ft)
    call system('chmod +x ' . expand('%'))
    return l:values[&ft]
  endif
  return a:key
endfunction

let g:snippets = {
      \ '_': {
      \   'today': {-> strftime('%Y-%m-%d')},
      \   'now': {-> strftime('%Y-%m-%dT%H:%M:%S')},
      \   '#!': function('Snippets_shebang'),
      \ },
      \ 'html': {
      \   'start': "<!DOCTYPE HTML>\n<html>\n<head>\n<meta http-equiv=\"Content-Type\" content=\"text/html; charset=utf-8\" />\n</head>\n<body>\n</body>\n</html>",
      \ },
      \ 'python': {
      \   'ipy': 'from IPython import embed; embed(colors="neutral")',
      \   'ipython': 'from IPython import embed; embed(colors="neutral")',
      \   'pdb': 'import pdb;pdb.set_trace()',
      \   'ipdb': 'import ipdb;ipdb.set_trace()',
      \   'pd': 'import pdb;pdb.set_trace()',
      \   'pp': 'from pprint import pprint ; pprint()',
      \   'main': "def main():\n\n\n\<BS>if __name__ == '__main__'\n    main()",
      \ },
      \ 'ruby': {
      \   'frozen': '# frozen_string_literal: true',
      \   'fro': '# frozen_string_literal: true',
      \   'pry': 'binding.irb',
      \   'irb': 'binding.irb',
      \   'pp': "require 'pp'; pp ",
      \   'rspec': {-> 'RSpec.describe ' . substitute(substitute('_' . expand('%:t'), '_spec.rb', '', 0), '_\([a-z]\)', '\U\1', 'g') . " do\n" .
      \                'subject(:' . substitute(expand('%:t'), '_spec.rb', '', 0) . ") { described_class.new() }\n" .
      \                'end'},
      \ },
      \}

function Snip()
  let l:col = col('.')
  if l:col < 2
    return "\<tab>"
  endif
  let l:search = getline('.')[0:max([0, l:col - 2])]

  for l:ft in [&ft, '_']
    if has_key(g:snippets, l:ft)
      let l:x = g:snippets[l:ft]
      for k in keys(l:x)
        if match(l:search, '\<' . k . '$') != -1
          if type(l:x[k]) == v:t_func
            let l:v = l:x[k](k)
          else
            let l:v = l:x[k]
          endif
          return repeat("\<BS>", len(k)) . l:v
        endif
      endfor
    endif
  endfor

  return "\<tab>"
endfunction

inoremap <silent><script><expr> <tab> Snip()

imap <silent><script><expr> <C-X><C-X> copilot#Accept("")
imap <silent><script><expr> <M-x> copilot#Accept("")
imap <M-n> <Plug>(copilot-next)
imap <M-p> <Plug>(copilot-previous)

let g:copilot_no_tab_map = v:true


""" Split line (sister to [J]oin lines)
nnoremap S i<cr><esc>^mwgk:silent! s/\v +$//<cr>:noh<cr>`w

nnoremap j gj
nnoremap k gk

nnoremap ü ]
nnoremap ä [
nnoremap gö g]
vnoremap gö g]

nnoremap gü :call tgrep#find_tag(expand("<cword>"))<CR>
vnoremap gü :call tgrep#find_tag(VisualSelection())<CR>

nnoremap K :call Rtfm(expand("<cword>"))<CR>
vnoremap K :call Rtfm(VisualSelection())<CR>

let g:no_man_maps = 1
autocmd BufEnter * if &readonly | nnoremap <buffer> q :q<cr> | endif

""" Vim-Tmux-Navigator mappings
let g:tmux_navigator_no_mappings = 0
if has('nvim')
  nnoremap <silent> <M-h> :TmuxNavigateLeft<cr>
  nnoremap <silent> <M-j> :TmuxNavigateDown<cr>
  nnoremap <silent> <M-k> :TmuxNavigateUp<cr>
  nnoremap <silent> <M-l> :TmuxNavigateRight<cr>
else
  nnoremap <silent> <ESC>h :TmuxNavigateLeft<cr>
  nnoremap <silent> <ESC>j :TmuxNavigateDown<cr>
  nnoremap <silent> <ESC>k :TmuxNavigateUp<cr>
  nnoremap <silent> <ESC>l :TmuxNavigateRight<cr>
endif

""" C-X C-G: relative version of file completions (C-X X-F)
let g:old_cwd = '.'
augroup file_completion_end
  autocmd!
  autocmd CompleteDone * execute "lcd " . g:old_cwd
augroup END
function! RelativeCompletion()
  let g:old_cwd = getcwd()
  lcd %:p:h
  call feedkeys("a\<C-X>\<C-F>")
endfunction
inoremap <C-X><C-G> <esc>:call RelativeCompletion()<cr>

inoremap <C-X><ü> <C-X><C-]>

onoremap in( :<c-u>normal! f(vi(<cr>
onoremap il( :<c-u>normal! F)vi(<cr>
onoremap in{ :<c-u>normal! f{vi{<cr>
onoremap il{ :<c-u>normal! F}vi{<cr>
onoremap in[ :<c-u>normal! f[vi[<cr>
onoremap il[ :<c-u>normal! F]vi[<cr>
onoremap in< :<c-u>normal! f<vi<<cr>
onoremap il< :<c-u>normal! F>vi<<cr>

xnoremap @ :<C-u>call ExecuteMacroOverVisualRange()<CR>

function! ExecuteMacroOverVisualRange()
  echo "@".getcmdline()
  execute ":'<,'>normal @".nr2char(getchar())
endfunction

" Commands -------------------------------------------------------------------

command! W w
command! Wq wq

command! Now :normal a<C-R>=strftime('%Y-%m-%dT%H:%M')<CR><Esc>
command! Today :normal a<C-R>=strftime('%Y-%m-%d')<CR><Esc>

command! -nargs=+ -complete=shellcmd Rtfm :call Rtfm("<args>")

" Status line ----------------------------------------------------------------

highlight User1 ctermfg=252 ctermbg=88 cterm=italic,bold

function! LinterStatus() abort
  let l:errors = len(v:lua.vim.diagnostic.get(0, { 'severity': 1 }))
  let l:warnings = len(v:lua.vim.diagnostic.get(0, { 'severity': 2 }))
  let l:result = []
  if l:errors > 0
    call add(l:result, printf('E:%d', l:errors))
  endif
  if l:warnings > 0
    call add(l:result, printf('W:%d', l:warnings))
  endif
  if empty(l:result)
    return ""
  endif
  return ' ' . join(l:result, ',') . ' '
endfunction

set statusline=%<%f\ %h%m%r\ %1*%{LinterStatus()}%*\ %{tagbar#currenttag('%s','','f')}%=%-14.(%l,%c%V%)\ %P

set laststatus=2  " always show status line

" Plugin Settings ------------------------------------------------------------

if !exists('g:loaded_matchit')
  runtime macros/matchit.vim
endif

let NERDTreeIgnore = ['__pycache__', '\.pyc$', '\.orig$']
let NERDTreeHighlightCursorline = 0

""" CoC

let g:coc_start_at_startup = 1
let g:coc_disable_startup_warning = g:coc_start_at_startup == 0
let g:coc_global_extensions = [
\ 'coc-diagnostic',
\ 'coc-eslint',
\ 'coc-jedi',
\ 'coc-json',
\ 'coc-prettier',
\ 'coc-solargraph',
\ '@yaegassy/coc-tailwindcss3',
\ 'coc-tsserver',
\ 'coc-clangd',
\]

highlight CocErrorSign ctermbg=1 ctermfg=7
highlight CocWarningSign ctermbg=3 ctermfg=0
highlight CocInfoSign ctermbg=4 ctermfg=0
highlight CocHintSign ctermbg=4 ctermfg=0
highlight default CocErrorHighlight ctermbg=12
highlight default CocWarningHighlight ctermbg=12
highlight default CocInfoHighlight ctermbg=12
highlight default CocHintHighlight ctermbg=12


""" LSP

lua << EOF
  -- https://github.com/neovim/nvim-lspconfig/blob/master/lsp/solargraph.lua
  vim.lsp.config['clangd'] = {
    cmd = { 'clangd', '--background-index', '--clang-tidy', '--clang-tidy-checks=*', '--completion-style=detailed' },
    -- "clangd",
    -- "--background-index",
    -- "-j=12",
    -- "--query-driver=/usr/bin/**/clang-*,/bin/clang,/bin/clang++,/usr/bin/gcc,/usr/bin/g++",
    -- "--clang-tidy",
    -- "--clang-tidy-checks=*",
    -- "--all-scopes-completion",
    -- "--cross-file-rename",
    -- "--completion-style=detailed",
    -- "--header-insertion-decorators",
    -- "--header-insertion=iwyu",
    -- "--pch-storage=memory",
    root_markers = { '.git', 'compile_commands.json' },
    filetypes = { 'c', 'cpp', 'objc' },
  }
  -- vim.lsp.config['solargraph'] = {
  --   cmd = { 'solargraph', 'stdio' },
  --   settings = {
  --     solargraph = {
  --       diagnostics = true,
  --     },
  --   },
  --   -- init_options = { formatting = true },
  --   filetypes = { 'ruby' },
  --   root_markers = { 'Gemfile', '.git' },
  -- }
  vim.lsp.config['ruby-lsp'] = {
    filetypes = { "ruby" },
    cmd = { "ruby-lsp" },
    root_markers = { "Gemfile", ".git" },
    init_options = {
      formatter = 'standard',
      linters = { 'standard' },
      -- indexing = {
      --   excludedPatterns = { "**/spec/**/*.rb" },
      -- },
    },
  }

  vim.lsp.enable({'clangd', 'lua_ls', 'ruby-lsp'})

  vim.api.nvim_create_autocmd('LspAttach', {
    callback = function(args)
      local client = vim.lsp.get_client_by_id(args.data.client_id)
      print('LSP attached: ' .. client.name)
      -- if client.server_capabilities.hoverProvider then
        vim.keymap.set('n', '<LocalLeader>h', vim.lsp.buf.hover, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.signatureHelpProvider then
        vim.keymap.set({ 'n' }, '<LocalLeader>ö', vim.lsp.buf.signature_help, { buffer = args.buf })
        -- vim.keymap.set({ 'i' }, '<LocalLeader>h', vim.lsp.buf.signature_help, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.declarationProvider then
        vim.keymap.set('n', 'gD', vim.lsp.buf.declaration, { buffer = args.buf })
        vim.keymap.set('n', '<LocalLeader>gD', vim.lsp.buf.declaration, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.definitionProvider then
        vim.keymap.set('n', 'gd', vim.lsp.buf.definition, { buffer = args.buf })
        vim.keymap.set('n', '<LocalLeader>gd', vim.lsp.buf.definition, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.typeDefinitionProvider then
        vim.keymap.set('n', '<LocalLeader>gt', vim.lsp.buf.type_definition, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.implementationProvider then
        vim.keymap.set('n', '<LocalLeader>gi', vim.lsp.buf.implementation, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.referencesProvider then
        vim.keymap.set('n', '<LocalLeader>gr', vim.lsp.buf.references, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.renameProvider then
        vim.keymap.set('n', '<LocalLeader>cr', vim.lsp.buf.rename, { buffer = args.buf })
      -- end
      -- if client.server_capabilities.codeActionProvider then
        vim.keymap.set('n', '<LocalLeader>a', vim.lsp.buf.code_action, { buffer = args.buf })
      -- end

      -- vim.keymap.set('n', ']d', vim.diagnostic.goto_next, { buffer = args.buf })
      -- vim.keymap.set('n', '[d', vim.diagnostic.goto_prev, { buffer = args.buf })
      vim.keymap.set('n', '<LocalLeader>d', vim.diagnostic.open_float, { buffer = args.buf })
      -- vim.keymap.set('n', '<space>', vim.diagnostic.open_float, { buffer = args.buf })
      vim.api.nvim_create_user_command('Dllist', vim.diagnostic.setloclist, {})
      vim.api.nvim_create_user_command('Dclist', vim.diagnostic.setqflist, {})
    end,
  })
EOF

""" TComment
let g:tcomment_mapleader1 = ""

""" tagbar
let g:tagbar_autofocus = 1
let g:tagbar_compact = 1
let g:tagbar_singleclick = 1

""" Ruby
let g:ruby_indent_block_style = 'do'
let g:ruby_indent_assignment_style = 'variable'

" Find and load local .vimrc -------------------------------------------------

call SourceLocalVimrc()
