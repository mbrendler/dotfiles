
test -z "$TMUX" && export TERM=xterm-256color

if test ! "$PROFILE_SOURCED" -eq 1 ; then
  if test -e "$HOME/.profile" ; then
    source "$HOME/.profile"
  fi
fi

typeset -U path
path=(
  $HOME/.bin
  $HOME/privateroot/usr/bin
  $HOME/privateroot/gnu/bin
  $HOME/.rvm/bin
  /usr/local/bin
  /usr/local/sbin
  $path
)

test -e "$HOME/.rvm/bin" && path=($path $HOME/.rvm/bin)

export VIRTUALENVWRAPPER_SCRIPT=/usr/local/bin/virtualenvwrapper.sh
if test -e $VIRTUALENVWRAPPER_SCRIPT ; then
  export WORKON_HOME=$HOME/.virtualenvs
  VIRTUALENVWRAPPER_SCRIPT_LAZY=${VIRTUALENVWRAPPER_SCRIPT%.sh}_lazy.sh
  if test -e $VIRTUALENVWRAPPER_SCRIPT_LAZY ; then
    source $VIRTUALENVWRAPPER_SCRIPT_LAZY
  else
    source $VIRTUALENVWRAPPER_SCRIPT
  fi
fi

fpath=($HOME/.zsh/site-functions $fpath)

# Prompt #####################################################################

autoload -U colors
colors

if test $UID -eq 0 ; then
  caretcolor=$fg_no_bold[red]
elif test ! -z "$SSH_CLIENT" ; then
  caretcolor=$fg_bold[yellow]
else
  caretcolor=$fg_bold[blue]
fi

if test ! 20 -lt "$(hostname | wc -m)" ; then
  prompt_start="%m"
fi

PROMPT="$prompt_start%{$caretcolor%}::%{$reset_color%}%{$fg_no_bold[green]%}%~ %{$caretcolor%}»%{$reset_color%} "
RPROMPT="%(?..%{$fg_no_bold[red]%}%? ↵%{$reset_color%})"

# Aliases and Functions ######################################################

if which dircolors > /dev/null 2> /dev/null ; then
  eval "$(dircolors -b)"
  alias ls="ls --color"
fi

CLICOLOR=1
LSCOLORS=Dxfxcxdxbxegedabagacad
CLICOLOR=LSCOLORS

alias .=source
alias grep="grep --color"
alias hlcat="source-highlight --output=STDOUT --out-format=esc -i"
alias b=bundle\ exec
alias pbpwd="pwd | pbcopy"
pbecho() { echo "$*" | pbcopy; }

if [[ $(uname) == "Linux" ]] ; then
  alias pbcopy='xsel --clipboard --input'
  alias pbpaste='xsel --clipboard --output'
elif [[ $(uname) == "Darwin" ]] ; then
  alias gvim=mvim
fi

function i {
  if test "$1" = cd ; then
    shift
    cd "$(command i dir "$@")"
  else
    command i "$@"
  fi
}

# History ####################################################################

setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt NO_INC_APPEND_HISTORY
setopt NO_SHARE_HISTORY
HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=10000

# Behavior ###################################################################

setopt BASH_AUTO_LIST
setopt NO_AUTO_MENU
setopt NO_ALWAYS_LAST_PROMPT
setopt NO_CORRECT
setopt NO_CORRECTALL
setopt AUTO_PUSHD
setopt NO_CDABLEVARS

# Completion: ################################################################

zmodload -i zsh/complist
zstyle :compinstall filename "$HOME/.zshrc"

# case insensitive completion:
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

zstyle ':completion:*' list-colors ''
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'
if [ "$OSTYPE[0,7]" = "solaris" ] ; then
  zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm"
else
  zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm -w -w"
fi

# disable named-directories autocompletion
zstyle ':completion:*:cd:*' tag-order local-directories directory-stack path-directories

# Use caching so that commands like apt and dpkg complete are useable
zstyle ':completion::complete:*' use-cache 1
zstyle ':completion::complete:*' cache-path $ZSH_CACHE_DIR

# Don't complete uninteresting users
zstyle ':completion:*:*:*:users' ignored-patterns \
        adm amanda apache at avahi avahi-autoipd beaglidx bin cacti canna \
        clamav daemon dbus distcache dnsmasq dovecot fax ftp games gdm \
        gkrellmd gopher hacluster haldaemon halt hsqldb ident junkbust kdm \
        ldap lp mail mailman mailnull man messagebus  mldonkey mysql nagios \
        named netdump news nfsnobody nobody nscd ntp nut nx obsrun openvpn \
        operator pcap polkitd postfix postgres privoxy pulse pvm quagga radvd \
        rpc rpcuser rpm rtkit scard shutdown squid sshd statd svn sync tftp \
        usbmux uucp vcsa wwwrun xfs '_*'

# ... unless we really want to.
zstyle '*' single-ignored show

autoload -Uz compinit
compinit

# syntax-highlighting ########################################################

ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
source "$HOME/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

# Key bindings ###############################################################

bindkey -e
bindkey '\e.' insert-last-word

bindkey '^W' vi-backward-kill-word

# Jump to next/last word by Alt+f / Alt+b:
bindkey ƒ forward-word
bindkey ∫ backward-word
if [[ $(uname) == "Darwin" ]] ; then
  # Delete Key (fn + Backspace) on OS X
  bindkey "^[[3~" delete-char
fi

##############################################################################

fortune -s 2> /dev/null || true
